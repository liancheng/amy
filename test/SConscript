import platform

Import('env')

# Only runs `lcov` on Mac.
run_coverage = platform.system() == 'Darwin'

lcov_flags = ['-fprofile-arcs', '-ftest-coverage'] if run_coverage else []

test_env = env.Clone()
test_env.AppendUnique(CCFLAGS=lcov_flags,
                      LINKFLAGS=lcov_flags,
                      LIBS='boost_unit_test_framework')

program = test_env.Program(target='test',
                           source=['async_connect_test.cpp',
                                   'main.cpp',
                                   'blocking_connect_test.cpp',
                                   'connector_test.cpp',
                                   'auth_info_test.cpp'])

test_source = program

if run_coverage:
    lcov_reset = env.LCov(target='lcov-reset',
                          source=program,
                          LCOVRESET=True,
                          LCOVDIR='.',
                          LCOVBASEDIR='#/')
    test_source.append(lcov_reset)

run_test = env.Command(target='run-test',
                       source=test_source,
                       action=program[0].abspath)

if run_coverage:
    env.GenHtml(target=Dir('report'),
                source=env.LCov(target='coverage.info',
                                source=run_test,
                                LCOVDIR='.',
                                LCOVBASEDIR='#/'))

    env.Clean(program,
              [env.Dir('report'),
               env.File('test.gcno')])

# vim:ft=python sw=4 ts=4 tw=80 et
